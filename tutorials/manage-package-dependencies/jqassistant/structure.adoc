[[structure:Default]]
[role=group,includesConstraints="structure:UndefinedComponentDependencies"]
== Structure

This document describes the rules regarding package dependencies.

[[structure:Component]]
[source,cypher,role=concept,requiresConcepts="maven:MainArtifact"]
.All packages in the root package "your.company.project" of the main artifact are labeled as `Component`.
----
MATCH
  (:Main:Artifact)-[:CONTAINS]->(root:Package)-[:CONTAINS]->(component:Package)
WHERE
  root.fqn = "your.company.project"
SET
  component:Component
RETURN
  component as Component
ORDER BY
  component.name desc
----

[[structure:DefinedComponentDependencies]]
[source,cypher,role=concept,requiresConcepts="structure:Component",verify=aggregation]
.Allowed dependencies between components are represented by relationships of type `DEFINES_DEPENDENCY`.
----
MATCH
  (a:Component{name:"a"}),
  (b:Component{name:"b"}),
  (c:Component{name:"c"})
MERGE
  (b)-[:DEFINES_DEPENDENCY]->(a)
MERGE
  (c)-[:DEFINES_DEPENDENCY]->(b)
RETURN
  count(*) as Count
----

[[structure:ComponentDependencies]]
[source,cypher,role=concept,requiresConcepts="structure:Component",reportType="graphml"]
.A component depends on another component if there is a dependency on type level between both. These dependencies are represented by relationships of type `DEPENDS_ON_COMPONENT`.
----
MATCH
  (c1:Component)-[:CONTAINS*]->(t1:Type),
  (c2:Component)-[:CONTAINS*]->(t2:Type),
  (t1)-[:DEPENDS_ON]->(t2)
WHERE
  c1 <> c2
WITH
  c1, c2, count(*) as weight
MERGE
  (c1)-[d:DEPENDS_ON_COMPONENT]->(c2)
SET
  d.weight = weight
RETURN
  c1 as Component, c2 as ComponentDependency, d as Dependency
----

[[structure:UndefinedComponentDependencies]]
[source,cypher,role=constraint,requiresConcepts="structure:DefinedComponentDependencies,structure:ComponentDependencies"]
.Dependencies between components are only allowed if there is a defined dependency between them.
----
MATCH
  (c1:Component)-[:DEPENDS_ON_COMPONENT]->(c2:Component)
WHERE NOT
  (c1:Component)-[:DEFINES_DEPENDENCY]->(c2:Component)
WITH
  c1, c2
MATCH
  (c1)-[:CONTAINS*]->(t1:Type),
  (c2)-[:CONTAINS*]->(t2:Type),
  (t1)-[:DEPENDS_ON]->(t2)
RETURN
  c1 as Component, t1 as Type, c2 as InvalidComponentDependency, collect(t2) as InvalidTypeDependencies
----
